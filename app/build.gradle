apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.google.firebase.appdistribution'
apply plugin: 'com.github.triplet.play'
apply from: '../release_notes.groovy'

import com.github.triplet.gradle.androidpublisher.ReleaseStatus

def getFirebaseDistributionKeyPath = { ->
    project.hasProperty('firebase_distribution') ? firebase_distribution : file("firebase-distribution.json").path
}

def getPlayCredentials = { ->
    project.hasProperty('play_store_credentials') ? play_store_credentials : file("play-store-credentials.json").path
}

def getKeystorePath = { ->
    project.hasProperty('keystore') ? keystore : file("keystore.jks").path
}

def getKeystorePassword = { ->
    project.hasProperty('keystore_password') ? keystore_password : ""
}

def getKeystoreKeyPassword = { ->
    project.hasProperty('keystore_key_password') ? keystore_key_password : ""
}

def getKeystoreKeyAlias = { ->
    project.hasProperty('keystore_key_alias') ? keystore_key_alias : ""
}

def getBuildNumber = { ->
    project.hasProperty('build_number') ? build_number : 0
}

def getEnableCrashlyticsDebug = { ->
    project.hasProperty('enable_crashlytics_debug') ? enable_crashlytics_debug : false
}

def getOutputDirectory = { ->
    project.hasProperty('output_directory') ? output_directory : "build/outputs/bundle/release"
}

android {
    compileSdkVersion 31
    buildToolsVersion "31.0.0"

    defaultConfig {
        applicationId "io.smileyjoe.putio.tv"
        minSdkVersion 26
        targetSdkVersion 31
        versionCode 36
        versionName "3.7.2." + getBuildNumber()
        buildConfigField "String", "PUTIO_CLIENT_ID", "\"${putio_client_id}\""
        buildConfigField "String", "PUTIO_CLIENT_SECRET", "\"${putio_client_secret}\""
        buildConfigField "String", "TMDB_AUTH_TOKEN", "\"${tmdb_auth_token}\""
        project.ext.set('host', 'tv.putio.smileyjoe.io')

    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        release {
            storeFile = file(getKeystorePath())
            storePassword getKeystorePassword()
            keyAlias getKeystoreKeyAlias()
            keyPassword getKeystoreKeyPassword()
        }
    }

    buildTypes {
        debug{
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            ext.enableCrashlytics = getEnableCrashlyticsDebug()
            resValue 'string', 'host_name', 'debug.' + project.ext.get('host')

            firebaseAppDistribution {
                artifactType="APK"
                releaseNotes = generateReleaseNotes("debug")
                groups="qa"
                serviceCredentialsFile = getFirebaseDistributionKeyPath()
            }
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            resValue 'string', 'host_name', project.ext.get('host')

            firebaseAppDistribution {
                artifactType="AAB"
                releaseNotes = generateReleaseNotes("release")
                groups="testers"
                serviceCredentialsFile = getFirebaseDistributionKeyPath()
            }
        }
    }

    buildFeatures {
        viewBinding true
    }
}

play {
    track.set("internal")
    releaseStatus.set(ReleaseStatus.COMPLETED)
    artifactDir.set(file(getOutputDirectory()))
    serviceAccountCredentials.set(file(getPlayCredentials()))
    defaultToAppBundles.set(true)
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation 'androidx.leanback:leanback:1.0.0'

    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.github.bumptech.glide:glide:4.13.0'
    implementation 'com.google.android.exoplayer:exoplayer:2.17.1'
    implementation 'com.google.android.exoplayer:extension-leanback:2.17.1'

    implementation 'com.github.HaarigerHarald:android-youtubeExtractor:master-SNAPSHOT'

    implementation 'com.koushikdutta.ion:ion:3.1.0'
    implementation "androidx.room:room-runtime:2.4.2"
    implementation 'com.facebook.stetho:stetho:1.5.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.tvprovider:tvprovider:1.0.0'
    annotationProcessor "androidx.room:room-compiler:2.4.2"

    implementation platform('com.google.firebase:firebase-bom:29.1.0')
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-analytics'

    implementation 'com.prof18.rssparser:rssparser:4.0.2'
}